// This script sets up our pom and artifacts for deployment.

apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'signing'

version = '1.3-SNAPSHOT'
group = 'com.caverock'

def siteUrl = 'https://github.com/BigBadaboom/androidsvg'
def gitUrl = 'https://github.com/BigBadaboom/androidsvg.git'

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'

                artifactId 'androidsvg-aar'
                name 'AndroidSVG'
                description 'SVG rendering library for Android.'
                url siteUrl

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }
                developers {
                    developer {
                        id 'BigBadaboom'
                        name 'Paul LeBeau'
                        email 'androidsvgfeedback@gmail.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

// By default, we will upload just our library AAR. Here we specify that we also want to upload our sources.
artifacts {
    archives sourcesJar
}

signing {
    sign configurations.archives
}
